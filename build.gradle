group 'de.melnichuk'
version '0.27.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.freemarker:freemarker:2.3.26-incubating'
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'com.beust:jcommander:1.72'
    testCompile 'junit:junit:4.12'
}

task generateHTML(type:JavaExec) {
    main = 'yevgeniy.melnichuk.prettyconfiguration.Main'
    classpath = sourceSets.main.runtimeClasspath
    args(['--template', 'src/test/resources/template.html',
        '--output-dir', './build/site/',
        '--output-name', 'index.html',
        '--input-files'] + getPropertiesFiles())

}

def getPropertiesFiles() {
    new File('src/test/resources').listFiles({ file -> file.name.endsWith('.properties') } as FileFilter).collect({ File file -> file.path})
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar, javadocJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'pretty-configuration'
                packaging 'jar'
                url 'https://github.com/yevmel/pretty-configuration'
                description 'human friendly representation of your applications configuration.'

                scm {
                    connection 'scm:git:git://github.com/yevmel/pretty-configuration.git'
                    developerConnection 'scm:git:ssh://github.com:yevmel/pretty-configuration.git'
                    url 'http://github.com/yevmel/pretty-configuration/tree/master'
                }

                licenses {
                    license {
                        name 'MIT'
                        url 'https://github.com/yevmel/pretty-configuration/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'yevmel'
                        name 'Yevgeniy Melnichuk'
                        email 'yevgeniy.melnichuk@googlemail.com'
                    }
                }
            }

        }
    }
}
